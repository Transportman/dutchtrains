/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_coaches_PlanW_blue_purchase, "gfx/purchase/coaches/PlanW_blue.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_coaches_PlanW_blue_banners_purchase, "gfx/purchase/coaches/PlanW_blue_banners.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_coaches_PlanW_benelux_purchase, "gfx/purchase/coaches/PlanW_benelux.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_coaches_PlanW_yellow_blue_purchase, "gfx/purchase/coaches/PlanW_yellow_blue.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_coaches_PlanW_purchase, current_year) {
	0..1969: spriteset_coaches_PlanW_blue_purchase;
	1970..1972: spriteset_coaches_PlanW_blue_banners_purchase;
	1973..1981: spriteset_coaches_PlanW_benelux_purchase;
    spriteset_coaches_PlanW_yellow_blue_purchase;
}

/*
* VEHICLE
*/

spriteset(spriteset_coaches_PlanW_blue, "gfx/coaches/PlanW.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_coaches_PlanW_blue_banners, "gfx/coaches/PlanW.png") {
	template_2cc_88(1, 32)
}
spriteset(spriteset_coaches_PlanW_benelux, "gfx/coaches/PlanW.png") {
	template_2cc_88(1, 64)
}
spriteset(spriteset_coaches_PlanW_yellow_blue, "gfx/coaches/PlanW.png") {
	template_2cc_88(1, 96)
}
spriteset(spriteset_coaches_PlanD_benelux_cab, "gfx/coaches/PlanD.png") {
	template_2cc_88(1, 64)
}


spriteset(spriteset_REV_coaches_PlanW_blue, "gfx/coaches/PlanW.png") {
	template_2cc_88r(1, 1)
}
spriteset(spriteset_REV_coaches_PlanW_blue_banners, "gfx/coaches/PlanW.png") {
	template_2cc_88r(1, 32)
}
spriteset(spriteset_REV_coaches_PlanW_benelux, "gfx/coaches/PlanW.png") {
	template_2cc_88r(1, 64)
}
spriteset(spriteset_REV_coaches_PlanW_yellow_blue, "gfx/coaches/PlanW.png") {
	template_2cc_88r(1, 96)
}
spriteset(spriteset_REV_coaches_PlanD_benelux_cab, "gfx/coaches/PlanD.png") {
	template_2cc_88(1, 96)
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, SELF, switch_coaches_PlanW_beneluxcab, position_in_consist_from_end) {
	0: spriteset_coaches_PlanD_benelux_cab;
	spriteset_coaches_PlanW_benelux;
}

switch(FEAT_TRAINS, PARENT, switch_coaches_PlanW_autolivery_national, date_of_last_service) {
	0..date(1969,12,31): spriteset_coaches_PlanW_blue;
	date(1970,1,1)..date(1981,12,31): spriteset_coaches_PlanW_blue_banners;
    spriteset_coaches_PlanW_yellow_blue;
}

switch(FEAT_TRAINS, PARENT, switch_coaches_PlanW_autolivery, vehicle_type_id) {
    item_electric_HLE25: switch_coaches_PlanW_beneluxcab;
    switch_coaches_PlanW_autolivery_national;
}

switch(FEAT_TRAINS, SELF, switch_coaches_PlanW_purchaselivery, build_year) {
	0..1969: spriteset_coaches_PlanW_blue;
	1970..1981: spriteset_coaches_PlanW_blue_banners;
    spriteset_coaches_PlanW_yellow_blue;
}

switch(FEAT_TRAINS, SELF, switch_for_coaches_PlanW_livery, cargo_subtype) {
	1: switch_coaches_PlanW_purchaselivery;
    2: spriteset_coaches_PlanW_blue;
    3: spriteset_coaches_PlanW_blue_banners;
    4: spriteset_coaches_PlanW_benelux;
    5: spriteset_coaches_PlanD_benelux_cab;
    6: spriteset_coaches_PlanW_yellow_blue;
    switch_coaches_PlanW_autolivery;
}

switch(FEAT_TRAINS, SELF, switch_REV_coaches_PlanW_beneluxcab, position_in_consist) {
	0: spriteset_REV_coaches_PlanD_benelux_cab;
	spriteset_REV_coaches_PlanW_benelux;
}

switch(FEAT_TRAINS, PARENT, switch_REV_coaches_PlanW_autolivery_national, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0xFFFFFFFF, 0x4B]]){
	0..date(1969,12,31): spriteset_REV_coaches_PlanW_blue;
	date(1970,1,1)..date(1981,12,31): spriteset_REV_coaches_PlanW_blue_banners;
    spriteset_REV_coaches_PlanW_yellow_blue;
}

switch(FEAT_TRAINS, PARENT, switch_REV_coaches_PlanW_autolivery, vehicle_type_id) {
    item_electric_HLE25: switch_REV_coaches_PlanW_beneluxcab;
    switch_REV_coaches_PlanW_autolivery_national;
}

switch(FEAT_TRAINS, SELF, switch_REV_coaches_PlanW_purchaselivery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0xFFFFFFFF, 0x49]]){
	0..1969: spriteset_REV_coaches_PlanW_blue;
	1970..1981: spriteset_REV_coaches_PlanW_blue_banners;
    spriteset_REV_coaches_PlanW_yellow_blue;
}

switch(FEAT_TRAINS, SELF, switch_rev_coaches_PlanW_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
	1: switch_REV_coaches_PlanW_purchaselivery;
    2: spriteset_REV_coaches_PlanW_blue;
    3: spriteset_REV_coaches_PlanW_blue_banners;
    4: spriteset_REV_coaches_PlanW_benelux;
    5: spriteset_REV_coaches_PlanD_benelux_cab;
    6: spriteset_REV_coaches_PlanW_yellow_blue;
    switch_REV_coaches_PlanW_autolivery;
}


// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_coaches_PlanW_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
    1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_blue);
    3: return string(str_cargo_subtype_blue_banners);
    4: return string(str_cargo_subtype_benelux);
    5: return string(str_cargo_subtype_benelux_plandcab);
    6: return string(str_cargo_subtype_yellow_blue_ic);
    return CB_RESULT_NO_TEXT;
}
