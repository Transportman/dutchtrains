/*
 *  Copyright (C) 2012-2019 Erato
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_emu_SNGIV_purchase, "gfx/purchase/emu/SNG.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_emu_SNGIV_yellow_purchase, "gfx/purchase/emu/SNG_yellow.png") {
	template_purchase(0, 0)
}
switch(FEAT_TRAINS, PARENT, switch_emu_SNGIV_purchase, current_year) {
	0..2021: spriteset_emu_SNGIV_purchase;
    spriteset_emu_SNGIV_yellow_purchase;
}

/*
* VEHICLE
*/
//NS
spriteset(spriteset_emu_SNGIV_front, "gfx/emu/SNG.png") {
	template_2cc_88(1,1)
}

spriteset(spriteset_emu_SNGIV_middle_pantograph, "gfx/emu/SNG.png") {
	template_2cc_68(1,32)
}

spriteset(spriteset_emu_SNGIV_middle, "gfx/emu/SNG.png") {
	template_2cc_68(1,64)
}

spriteset(spriteset_emu_SNGIV_back, "gfx/emu/SNG.png") {
	template_2cc_88(1,96)
}

switch(FEAT_TRAINS, SELF, switch_emu_SNGIV_position, (position_in_articulated_veh)) { 
	0: spriteset_emu_SNGIV_front;
	1: spriteset_emu_SNGIV_middle_pantograph;
	2: spriteset_emu_SNGIV_middle;
	spriteset_emu_SNGIV_back;
}
//NS
spriteset(spriteset_emu_SNGIV_yellow_front, "gfx/emu/SNG_yellow.png") {
	template_2cc_88(1,1)
}

spriteset(spriteset_emu_SNGIV_yellow_middle_pantograph, "gfx/emu/SNG_yellow.png") {
	template_2cc_68(1,32)
}

spriteset(spriteset_emu_SNGIV_yellow_middle, "gfx/emu/SNG_yellow.png") {
	template_2cc_68(1,64)
}

spriteset(spriteset_emu_SNGIV_yellow_back, "gfx/emu/SNG_yellow.png") {
	template_2cc_88(1,96)
}

switch(FEAT_TRAINS, SELF, switch_emu_SNGIV_yellow_position, (position_in_articulated_veh)) { 
	0: spriteset_emu_SNGIV_yellow_front;
	1: spriteset_emu_SNGIV_yellow_middle_pantograph;
	2: spriteset_emu_SNGIV_yellow_middle;
	spriteset_emu_SNGIV_yellow_back;
}

switch(FEAT_TRAINS, SELF, switch_emu_SNGIV_randomlivery, current_year) { 
	0..2021: switch_emu_SNGIV_position;
	switch_emu_SNGIV_yellow_position;
}

//Livery Switch Sets
switch(FEAT_TRAINS, SELF, switch_emu_SNGIV_livery, cargo_subtype){
    0: 	switch_emu_SNGIV_randomlivery;
	1:	switch_emu_SNGIV_position;
    2:	switch_emu_SNGIV_yellow_position;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_emu_SNGIV_subtype_text, cargo_subtype){
	0: return string(str_cargo_subtype_auto);
    1: return string(str_cargo_subtype_ns_sprinter);
    2: return string(str_cargo_subtype_ns_sprinter_yellow);
	return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_emu_SNGIV_articulated, extra_callback_info1) {
    1..3: return item_emu_SNGIV; //use same vehicle for all parts
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS; //stop adding vehicle parts
}

/*
* VISUAL EFFECT AND POWERED CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_emu_SNGIV_visual_effect_and_powered, (position_in_articulated_veh)) {
    1: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 0, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

/*
* Length
*/
switch(FEAT_TRAINS, SELF, switch_emu_SNGIV_length, (position_in_articulated_veh)) { 
	1..2: 6;
	8;
}