/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_electric_NS1200_purchase_turquoise, "gfx/purchase/electric/NS1200_turquoise.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_electric_NS1200_purchase_brown, "gfx/purchase/electric/NS1200_brown.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_electric_NS1200_purchase_blue, "gfx/purchase/electric/NS1200_blue.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_electric_NS1200_purchase_grey, "gfx/purchase/electric/NS1200_grey.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_electric_NS1200_purchase_vos, "gfx/purchase/electric/NS1200_vos.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_electric_NS1200_purchase_acts, "gfx/purchase/electric/NS1200_acts.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_electric_NS1200_purchase_eetc, "gfx/purchase/electric/NS1200_eetc.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_electric_NS1200_purchase, current_year) {
	0..1952: spriteset_electric_NS1200_purchase_turquoise;
    1953..1954: spriteset_electric_NS1200_purchase_brown;
    1955..1969: spriteset_electric_NS1200_purchase_blue;
    1970..1998: spriteset_electric_NS1200_purchase_grey;
    1999..1999: spriteset_electric_NS1200_purchase_acts;
    2000..2002: spriteset_electric_NS1200_purchase_vos;
    2003..2010: spriteset_electric_NS1200_purchase_acts;
    spriteset_electric_NS1200_purchase_eetc;
}

/*
* VEHICLE
*/

spriteset(spriteset_electric_NS1200_turqoise, "gfx/electric/NS1200.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_electric_NS1200_brown, "gfx/electric/NS1200.png") {
	template_2cc_88(1, 32)
}
spriteset(spriteset_electric_NS1200_blue, "gfx/electric/NS1200.png") {
	template_2cc_88(1, 64)
}
spriteset(spriteset_electric_NS1200_grey, "gfx/electric/NS1200.png") {
	template_2cc_88(1, 96)
}
spriteset(spriteset_electric_NS1200_ACTS, "gfx/electric/NS1200.png") {
	template_2cc_88(1, 128)
}
spriteset(spriteset_electric_NS1200_VOSlogistics, "gfx/electric/NS1200.png") {
	template_2cc_88(1, 160)
}
spriteset(spriteset_electric_NS1200_EETC, "gfx/electric/NS1200.png") {
	template_2cc_88(1, 192)
}

/*
* LIVERY SELECTION
*/

random_switch(FEAT_TRAINS, SELF, switch_electric_NS1200_randomlivery_actsvos) {
	1: spriteset_electric_NS1200_VOSlogistics;
    4: spriteset_electric_NS1200_ACTS;
}

switch(FEAT_TRAINS, SELF, switch_electric_NS1200_livery_ACTS_EETC, (bitmask_consist_info & bitmask(0))) {
    bitmask(0): spriteset_electric_NS1200_EETC; //if passenger carriage attached
    spriteset_electric_NS1200_ACTS;
}

switch(FEAT_TRAINS, SELF, switch_electric_NS1200_livery_cargo_EETC, (bitmask_consist_info & bitmask(0))) {
    bitmask(0): spriteset_electric_NS1200_EETC; //if passenger carriage attached
    switch_electric_NS1200_randomlivery_actsvos;
}
switch(FEAT_TRAINS, SELF, switch_electric_NS1200_livery_grey_cargo, (bitmask_consist_info & bitmask(0))) {
    bitmask(0): spriteset_electric_NS1200_grey; //if passenger carriage attached
    switch_electric_NS1200_randomlivery_actsvos;
}

switch(FEAT_TRAINS, SELF, switch_electric_NS1200_livery_cargopassenger, build_year) { //service 2011+
	0..2002: switch_electric_NS1200_livery_cargo_EETC; //cargo, or EETC depending on wagons attached
    2003..2010: switch_electric_NS1200_livery_ACTS_EETC; //ACTS, or EETC depending on wagons attached
    spriteset_electric_NS1200_EETC ;//EETC
}

switch(FEAT_TRAINS, SELF, switch_electric_NS1200_livery_passengercargo, build_year) { //service 1999-2010
	0..1998: switch_electric_NS1200_livery_grey_cargo; //ns-grey, or cargo depending on wagons attached
    1999..2002: switch_electric_NS1200_randomlivery_actsvos; //cargo, randomized
    spriteset_electric_NS1200_ACTS; //ACTS
}

random_switch(FEAT_TRAINS, SELF, switch_electric_NS1200_randomlivery_turqoisebrown) {
	14: spriteset_electric_NS1200_turqoise;
    11: spriteset_electric_NS1200_brown;
}

switch(FEAT_TRAINS, SELF, switch_electric_NS1200_autolivery, date_of_last_service) {
	0..date(1952,12,31): spriteset_electric_NS1200_turqoise;
    date(1953,1,1)..date(1954,12,31): switch_electric_NS1200_randomlivery_turqoisebrown;
    date(1955,1,1)..date(1969,12,31): spriteset_electric_NS1200_blue;
    date(1970,1,1)..date(1998,12,31): spriteset_electric_NS1200_grey;
    date(1999,1,1)..date(2010,12,31): switch_electric_NS1200_livery_passengercargo;
    switch_electric_NS1200_livery_cargopassenger;
}

switch(FEAT_TRAINS, SELF, switch_electric_NS1200_purchaselivery, build_year) {
	0..1952: spriteset_electric_NS1200_turqoise;
    1953..1954: switch_electric_NS1200_randomlivery_turqoisebrown;
    1955..1969: spriteset_electric_NS1200_blue;
    1970..1998: spriteset_electric_NS1200_grey;
    1999..2002: switch_electric_NS1200_randomlivery_actsvos;
    2003..2010: spriteset_electric_NS1200_ACTS;
    spriteset_electric_NS1200_EETC;
}

switch(FEAT_TRAINS, SELF, switch_electric_NS1200_livery, cargo_subtype) {
	1: switch_electric_NS1200_purchaselivery;
    2: spriteset_electric_NS1200_turqoise;
    3: spriteset_electric_NS1200_brown;
    4: spriteset_electric_NS1200_blue;
    5: spriteset_electric_NS1200_grey;
    6: spriteset_electric_NS1200_ACTS;
    7: spriteset_electric_NS1200_VOSlogistics;
    8: spriteset_electric_NS1200_EETC;
    switch_electric_NS1200_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_electric_NS1200_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_turquoise);
    3: return string(str_cargo_subtype_brown);
    4: return string(str_cargo_subtype_blue);
    5: return string(str_cargo_subtype_grey);
    6: return string(str_cargo_subtype_ACTS);
    7: return string(str_cargo_subtype_VOSlogistics);
    8: return string(str_cargo_subtype_EETC);
    return CB_RESULT_NO_TEXT;
}
