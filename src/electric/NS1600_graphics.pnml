/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_electric_NS1600_purchase, "gfx/purchase/electric/NS1700.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_electric_NS1600_purchase_red, "gfx/purchase/electric/NS1600_red.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_electric_NS1600_purchase_husa, "gfx/purchase/electric/NS1600_husa.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_electric_NS1600_purchase_locon, "gfx/purchase/electric/NS1600_locon.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_electric_NS1600_purchase, current_year) {
	0..1998: spriteset_electric_NS1600_purchase;
    1999..2003: spriteset_electric_NS1600_purchase_red;
    2004..2010: spriteset_electric_NS1600_purchase;
    2011..2015: spriteset_electric_NS1600_purchase_husa;
    spriteset_electric_NS1600_purchase_locon;
}

/*
* VEHICLE
*/

//yellow
spriteset(spriteset_electric_NS1600_yellow, "gfx/electric/NS1600.png") {
	template_2cc_88(1, 1)
}
//red
spriteset(spriteset_electric_NS1600_NSCargo, "gfx/electric/NS1600.png") {
	template_2cc_88(1, 32)
}
//husa
spriteset(spriteset_electric_NS1600_HUSA, "gfx/electric/NS1600.png") {
	template_2cc_88(1, 64)
}
//db schenker
spriteset(spriteset_electric_NS1600_DBSchenker, "gfx/electric/NS1600.png") {
	template_2cc_88(1, 96)
}
//Locon
spriteset(spriteset_electric_NS1600_LOCON, "gfx/electric/NS1600.png") {
	template_2cc_88(1, 128)
}
//Dinner Train
spriteset(spriteset_electric_NS1600_DinnerTrain, "gfx/electric/NS1600.png") {
	template_2cc_88(1, 160)
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, SELF, switch_electric_NS1600_livery_NS_NSC, (bitmask_consist_info & bitmask(0))) {
    bitmask(0): spriteset_electric_NS1600_yellow; //if passenger carriage attached
    spriteset_electric_NS1600_NSCargo;
}

switch(FEAT_TRAINS, SELF, switch_electric_NS1600_livery_NS_HUSA, (bitmask_consist_info & bitmask(0))) {
    bitmask(0): spriteset_electric_NS1600_yellow; //if passenger carriage attached
    spriteset_electric_NS1600_HUSA;
}

switch(FEAT_TRAINS, SELF, switch_electric_NS1600_livery_NS_LOCON, (bitmask_consist_info & bitmask(0))) {
    bitmask(0): spriteset_electric_NS1600_yellow; //if passenger carriage attached
    spriteset_electric_NS1600_LOCON;
}

switch(FEAT_TRAINS, SELF, switch_electric_NS1600_autolivery, date_of_last_service) {
	0..date(1998,12,31): spriteset_electric_NS1600_yellow;
    date(1999,1,1)..date(2003,12,31): switch_electric_NS1600_livery_NS_NSC;
    date(2004,1,1)..date(2010,12,31): spriteset_electric_NS1600_yellow;
    date(2011,1,1)..date(2015,12,31): switch_electric_NS1600_livery_NS_HUSA;
    switch_electric_NS1600_livery_NS_LOCON;
}

switch(FEAT_TRAINS, SELF, switch_electric_NS1600_purchaselivery, build_year) {
	0..1998: spriteset_electric_NS1600_yellow;
    1999..2003: switch_electric_NS1600_livery_NS_NSC;
    2004..2010: spriteset_electric_NS1600_yellow;
    2011..2015: switch_electric_NS1600_livery_NS_HUSA;
    switch_electric_NS1600_livery_NS_LOCON;
}

switch(FEAT_TRAINS, SELF, switch_electric_NS1600_livery, cargo_subtype) {
	1: switch_electric_NS1600_purchaselivery;
    2: spriteset_electric_NS1600_yellow;
    3: spriteset_electric_NS1600_NSCargo;
    4: spriteset_electric_NS1600_HUSA;
    5: spriteset_electric_NS1600_DBSchenker;
    6: spriteset_electric_NS1600_LOCON;
    7: spriteset_electric_NS1600_DinnerTrain;
    switch_electric_NS1600_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_electric_NS1600_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_yellow);
    3: return string(str_cargo_subtype_NSCargo);
    4: return string(str_cargo_subtype_HUSA);
    5: return string(str_cargo_subtype_DBSchenker);
    6: return string(str_cargo_subtype_Locon);
    7: return string(str_cargo_subtype_DinnerTrain);
    return CB_RESULT_NO_TEXT;
}
